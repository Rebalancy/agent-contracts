set dotenv-load
set dotenv-filename := "../.env"
set export

# rpc urls
RPC_URL_BASE_SEPOLIA := "https://base-sepolia.g.alchemy.com/v2/$ALCHEMY_API_KEY"
RPC_URL_ARBITRUM_SEPOLIA := "https://arb-sepolia.g.alchemy.com/v2/$ALCHEMY_API_KEY"
RPC_URL_OPTIMISM_SEPOLIA := "https://opt-sepolia.g.alchemy.com/v2/$ALCHEMY_API_KEY"
RPC_URL_ETHEREUM_SEPOLIA := "https://sepolia.infura.io/v3/$ALCHEMY_API_KEY"
RPC_URL_ETHEREUM_MAINNET := "https://eth-mainnet.g.alchemy.com/v2/$ALCHEMY_API_KEY"
RPC_URL_LOCAL := "http://localhost:8555"

# deployments
deploy_aave_vault JSON_RPC_URL SENDER:
    echo "Deploying Aave Vault"
    forge script script/001_deploy_aave_vault.s.sol:DeployAaveVaultScript --rpc-url $JSON_RPC_URL --sender $SENDER --broadcast --ffi -vvvv
    
deploy_mock_usdc JSON_RPC_URL SENDER:
    echo "Deploying MockUSDC"
    forge script script/000_deploy_mock_usdc.s.sol:DeployMockUSDCScript --rpc-url $JSON_RPC_URL --sender $SENDER --broadcast --ffi -vvvv

# deployment by network
deploy_local:
    echo "Deploying contracts locally"
    MNEMONIC=$MNEMONIC_LOCAL just deploy_mock_usdc $RPC_URL_LOCAL $SENDER_LOCAL
    MNEMONIC=$MNEMONIC_LOCAL just deploy_aave_vault $RPC_URL_LOCAL $SENDER_LOCAL
    MNEMONIC=$MNEMONIC_TESTNET just mint_usdc $RPC_URL_LOCAL $SENDER_LOCAL
    MNEMONIC=$MNEMONIC_TESTNET just deposit $RPC_URL_LOCAL $SENDER_LOCAL

deploy_base_sepolia:
    echo "Deploying contracts to Base Sepolia"
    MNEMONIC=$MNEMONIC_TESTNET just deploy_aave_vault $RPC_URL_BASE_SEPOLIA $SENDER_TESTNET
    MNEMONIC=$MNEMONIC_TESTNET just deposit $RPC_URL_BASE_SEPOLIA $SENDER_TESTNET

deploy_arbitrum_sepolia:
    echo "Deploying contracts to Arbitrum Sepolia"
    MNEMONIC=$MNEMONIC_TESTNET just deploy_aave_vault {{RPC_URL_ARBITRUM_SEPOLIA}} $SENDER_TESTNET
    MNEMONIC=$MNEMONIC_TESTNET just deposit {{RPC_URL_ARBITRUM_SEPOLIA}} $SENDER_TESTNET

# actions
send_usdc_to_agent_address_in_arbitrum_sepolia:
    echo "Transferring USDC to the Agent Address"
    MNEMONIC=$MNEMONIC_TESTNET forge script script/005_transfer_usdc.s.sol:TransferUSDCScript --rpc-url {{RPC_URL_ARBITRUM_SEPOLIA}} --sender $SENDER_TESTNET --broadcast --ffi -vvvv

seed_agent_address_in_optimism_sepolia:
    echo "Transferring ETH to the Agent Address"
    MNEMONIC=$MNEMONIC_TESTNET forge script script/004_transfer_eth.s.sol:TransferETHScript --rpc-url {{RPC_URL_OPTIMISM_SEPOLIA}} --sender $SENDER_TESTNET --broadcast --ffi -vvvv

seed_agent_address_in_arbitrum_sepolia:
    echo "Transferring ETH to the Agent Address"
    MNEMONIC=$MNEMONIC_TESTNET forge script script/004_transfer_eth.s.sol:TransferETHScript --rpc-url {{RPC_URL_ARBITRUM_SEPOLIA}} --sender $SENDER_TESTNET --broadcast --ffi -vvvv

mint_usdc JSON_RPC_URL SENDER:
    echo "Minting Mock USDC"
    forge script script/002_mint_usdc.s.sol:MintMockUSDCScript --rpc-url $JSON_RPC_URL --sender $SENDER --broadcast --ffi -vvvv

deposit JSON_RPC_URL SENDER:
    echo "Making deposit to Aave Vault"
    forge script script/003_deposit.s.sol:DepositScript --rpc-url $JSON_RPC_URL --sender $SENDER --broadcast --ffi -vvvv

# anvil
start_anvil:
    echo "Starting Anvil"
    anvil --port 8555 --chain-id 1337 --mnemonic "$MNEMONIC_LOCAL"

# forge
compile: 
    echo "Compiling contracts"
    forge build

# testing
test_unit:
    echo "Running unit tests"
    forge test --match-path "test/unit/**/*.sol" --rpc-url $RPC_URL_ETHEREUM_MAINNET -vvvv

test_fork:
    echo "Running fork tests"
    forge test --match-path "test/fork/**/*.sol" --fork-url $RPC_URL_ARBITRUM_SEPOLIA --fork-block-number 186031120 -vvvv 

test_debug:
    echo "Running debug tests"
    forge test --match-path "test/debug/**/*.sol" --fork-url {{RPC_URL_ARBITRUM_SEPOLIA}} --fork-block-number 202108155 -vvvv 

test_coverage:
    forge coverage --rpc-url $RPC_URL_ETHEREUM_MAINNET --report lcov 
    lcov --remove ./lcov.info --output-file ./lcov.info 'script' 'DeployerUtils.sol' 'DeploymentUtils.sol' 'config/*' 'helpers/*'
    genhtml lcov.info -o coverage --branch-coverage --ignore-errors category

test CONTRACT:
    forge test --mc {{CONTRACT}} --ffi -vvvv

test_only CONTRACT TEST:
    forge test --mc {{CONTRACT}} --mt {{TEST}} --rpc-url $RPC_URL_ETHEREUM_MAINNET --ffi -vvvv

# formatting
format: 
    echo "Formatting contracts"
    forge fmt

format-check:
    echo "Checking contract formatting"
    forge fmt --check

# export abis
export_abis:
    mkdir -p abis
    jq '.abi' out/AaveVault.sol/AaveVault.json > abis/AaveVault.abi.json
    jq '.abi' out/MockUSDC.sol/MockUSDC.json > abis/MockUSDC.abi.json
